plugins {
	id 'org.springframework.boot' version '2.7.1'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
}

group = 'org.mifos'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenCentral()

	maven {
		url = uri('https://fynarfin.jfrog.io/artifactory/fyn-libs-snapshot-local/')
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	testImplementation 'org.mifos:ph-ee-connector-common:1.0.0-SNAPSHOT'

	testImplementation "com.google.truth:truth:1.1.3"
	testImplementation 'com.google.code.gson:gson:2.9.0'
	testImplementation('io.rest-assured:rest-assured:5.1.1') {
		exclude group: 'org.codehaus.groovy'
	}

	implementation 'io.cucumber:cucumber-java:7.8.1'
	implementation 'io.cucumber:cucumber-spring:7.8.1'
	testImplementation 'io.cucumber:cucumber-junit:7.8.1'
}

tasks.named('test') {
	useJUnitPlatform()
}

configurations {
	cucumberRuntime {
		extendsFrom testImplementation
	}
}

task cucumberCli() {
	dependsOn assemble, testClasses
	doLast {
		javaexec {
			main = "io.cucumber.core.cli.Main"
			classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
			args = [
					'--plugin', 'html:cucumber-report',
					'--plugin', 'json:cucumber.json',
					'--plugin', 'pretty',
					'--plugin', 'html:build/cucumber-report.html',
					'--plugin', 'json:build/cucumber-report.json',
					'--glue', 'org.mifos.integrationtest.cucumber',
					'src/test/java/resources']
		}
	}
}
